global with sharing class VolunteerJobsService {
	private static VolunteerJobsService instance;
	private static Boolean allowConstruct = false;
	
	//throws error if attempted to acces instance through constructor
	global VolunteerJobsService() {
		if(!allowConstruct){
			throw new SingletonException(VolunteerJobsService.class);
		}
	}
	
	/*
	* Output: VolunteerJobsService -instance of the class 
	* Description: This method allows the Object To be accessed from 
	* 	a static context, And allows instance methods to be called on it
	*/
	global static VolunteerJobsService getInstance() {
		if(instance == null) {
			allowConstruct = true;
			instance = new VolunteerJobsService();
			allowConstruct = false;
		}
		return instance;
	}
	
	/*
	* Description:
	*	Allows a mock instance to be set by unit tests.
	*/
	@TestVisible
	private static void setMock(VolunteerJobsService i){
		instance = i;
	}
	
	/*
	* Description:
	*	This method provides access to the service layer's setMongoId 
	*/
	global void setMongoIds(List<GW_Volunteers__Volunteer_Job__c> updtJobs) {
		service().setMongoIds(updtJobs);
	}
	
	/*
	* Output:
	* 	IVolunteerJobsService - an instance of VolunteerJobsServiceImpl cast as it's interface
	* Description:
	* 	This method provides access to VolunteerJobsServiceImpl class defined in the app
	*/
	private IVolunteerJobsService service() {
		return (IVolunteerJobsService)App.Service.newInstance(IVolunteerJobsService.Class);
	}

    /*
	* Output:
	* 	ICampaignsService - an instance of CampaignsSelector cast as it's interface
	* Description:
	* 	This method provides access to CampaignsSelector class defined in the app
	*/
	private VolunteerJobsSelector selector() {
		return VolunteerJobsSelector.newInstance();
	}
	
	/*
	* Interface implemented by domain classes that support the ability to produce volunteerJobDTOs
	*/
	public interface ISupportVolunteerJobDTO {
		void generate(VolunteerJobDTOFactory factory);
	}
	
	/*
	* Creates a list of VolunteerJobDTO's so that the standard SOBject garbage will be stripped off when 
	* serializing to json for use in web services.  And the entire object is not being exposed.
	*/
	public class VolunteerJobDTOFactory {
		/*
		* List of jobDTOs generated by the factory
		*/
		public List<VolunteerJobDTO> volunteerJobDTOs {get; private set;}
		
		public VolunteerJobDTOFactory() {
			this.volunteerJobDTOs = new List<VolunteerJobDTO>();
		}
		
		/*
		* Converts a volunteerJob object into a VolunteerJobDTO and adds to the list.
		*/
		public void add(GW_Volunteers__Volunteer_Job__c job){
			this.volunteerJobDTOs.add(new VolunteerJobDTO(job));
		}		
	}
	
	global class VolunteerJobDTO {
		public String name;
		public String campaignId;
		public String jobId;
		public String description;
		public String skills;
		public Boolean displayOnWebsite;
		public Boolean active;
		public Boolean ongoing;
		public Integer numberOfVolunteersStillNeeded;
		public Integer nubmerOfShifts;
		public VolunteerJobLocation location;
		
		public VolunteerJobDTO(GW_Volunteers__Volunteer_Job__c job) {
			this.name = job.name;
			this.campaignId = job.GW_Volunteers__Campaign__c;
			this.jobId = job.Id;
			this.description = job.GW_Volunteers__Description__c;
			this.skills = job.GW_Volunteers__Skills_Needed__c;
			this.displayOnWebsite = job.GW_Volunteers__Display_on_Website__c;
			this.active = !job.GW_Volunteers__Inactive__c;
			this.ongoing = job.GW_Volunteers__Ongoing__c;
			this.numberOfVolunteersStillNeeded = (Integer)job.GW_Volunteers__Number_of_Volunteers_Still_Needed__c;
			this.nubmerOfShifts = (Integer)job.GW_Volunteers__Number_of_Shifts__c;
			this.location = new VolunteerJobLocation(job);
		}
	}
	
	global class VolunteerJobLocation {
		public String street;
		public String city;
		public String state;
		public String zip;
		public String geocode;
		public String information;
		
		public VolunteerJobLocation(GW_Volunteers__Volunteer_Job__c job) {
			this.street = job.GW_Volunteers__Location_Street__c;
			this.city = job.GW_Volunteers__Location_City__c;
			this.state = job.GW_Volunteers__Location__c;
			this.zip = job.GW_Volunteers__Location_Zip_Postal_Code__c;
			this.information = job.GW_Volunteers__Location_Information__c;
		}
	}
}
