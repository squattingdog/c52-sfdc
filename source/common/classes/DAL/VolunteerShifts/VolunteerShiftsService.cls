global with sharing class VolunteerShiftsService {
    private static VolunteerShiftsService instance;
	private static Boolean allowConstruct = false;
	
	//throws error if attempted to acces instance through constructor
	global VolunteerShiftsService() {
		if(!allowConstruct){
			throw new SingletonException(VolunteerShiftsService.class);
		}
	}
	
	/*
	* Output: VolunteerShiftsService -instance of the class 
	* Description: This method allows the Object To be accessed from 
	* 	a static context, And allows instance methods to be called on it
	*/
	global static VolunteerShiftsService getInstance() {
		if(instance == null) {
			allowConstruct = true;
			instance = new VolunteerShiftsService();
			allowConstruct = false;
		}
		return instance;
	}
	
	/*
	* Description:
	*	Allows a mock instance to be set by unit tests.
	*/
	@TestVisible
	private static void setMock(VolunteerShiftsService i){
		instance = i;
	}
	
	/*
	* Description:
	*	This method provides access to the service layer's setMongoId 
	*/
	global void setMongoIds(List<GW_Volunteers__Volunteer_Shift__c> updtShifts) {
		service().setMongoIds(updtShifts);
	}
	
	/*
	* Output:
	* 	IVolunteerShiftsService - an instance of VolunteerShiftsServiceImpl cast as it's interface
	* Description:
	* 	This method provides access to VolunteerShiftsServiceImpl class defined in the app
	*/
	private IVolunteerShiftsService service() {
		return (IVolunteerShiftsService)App.Service.newInstance(IVolunteerShiftsService.Class);
	}

    /*
	* Output:
	* 	ICampaignsService - an instance of CampaignsSelector cast as it's interface
	* Description:
	* 	This method provides access to CampaignsSelector class defined in the app
	*/
	private VolunteerShiftsSelector selector() {
		return VolunteerShiftsSelector.newInstance();
	}


    /*
	* Interface implemented by domain classes that support the ability to produce volunteerShiftDTOs
	*/
	public interface ISupportVolunteerShiftDTO {
		void generate(VolunteerShiftDTOFactory factory);
	}
	
	/*
	* Creates a list of VolunteerShiftDTO's so that the standard SOBject garbage will be stripped off when 
	* serializing to json for use in web services.  And the entire object is not being exposed.
	*/
	public class VolunteerShiftDTOFactory {
		/*
		* List of ShiftDTOs generated by the factory
		*/
		public List<VolunteerShiftDTO> volunteerShiftDTOs {get; private set;}
		
		public VolunteerShiftDTOFactory() {
			this.volunteerShiftDTOs = new List<VolunteerShiftDTO>();
		}
		
		/*
		* Converts a volunteerShift object into a VolunteerShiftDTO and adds to the list.
		*/
		public void add(GW_Volunteers__Volunteer_Shift__c shift){
			this.volunteerShiftDTOs.add(new VolunteerShiftDTO(shift));
		}		
	}
	
	global class VolunteerShiftDTO {
		public String name;
        public string shiftId;
		public String jobId;
		public String description;
        public DateTime startDateTime;
        public Decimal duration;
		public Boolean active;
		public Integer numberOfVolunteersStillNeeded;
        public Integer desiredNumberOfVolunteers;
        public Integer totalVolunteers;
		public VolunteerJobsService.VolunteerJobLocation location;
		
		public VolunteerShiftDTO(GW_Volunteers__Volunteer_Shift__c shift) {
			this.name = shift.name;
            this.shiftId = shift.id;
			this.jobId = shift.GW_Volunteers__Volunteer_Job__c;
			this.description = shift.GW_Volunteers__Description__c;
            this.startDateTime = shift.GW_Volunteers__Start_Date_Time__c;
            this.duration = shift.GW_Volunteers__Duration__c;
			this.active = !shift.IsDeleted;
			this.numberOfVolunteersStillNeeded = (Integer)shift.GW_Volunteers__Number_of_Volunteers_Still_Needed__c;
            this.desiredNumberOfVolunteers = (Integer)shift.GW_Volunteers__Desired_Number_of_Volunteers__c;
            this.totalVolunteers = (Integer)shift.GW_Volunteers__Total_Volunteers__c;
			this.location = new VolunteerJobsService.VolunteerJobLocation(shift);
		}
	}
}
